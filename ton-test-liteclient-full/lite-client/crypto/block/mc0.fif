"Asm.fif" include

def? $1 { @' $1 } { "" } cond constant suffix 
{ suffix $+ } : +suffix

wc_master setworkchain
-17 setglobalid   // negative value means a test instance of the blockchain

// Initial state of Workchain 0 (Basic workchain)

0 mkemptyShardState 

cr ."initial basechain state is:" cr dup <s csr. cr
dup dup 31 boc+>B dup Bx. cr
dup "basestate0" +suffix +".boc" B>file
Bhash tuck ."file hash=" dup x. cr 256 u>B "basestate0" +suffix +".fhash" B>file
hash tuck ."root hash=" dup x. cr 256 u>B "basestate0" +suffix +".rhash" B>file

now 0 2 60 0 add-std-workchain
config.workchains!

// SmartContract #1 (Simple wallet)

<{  SETCP0 DUP IFNOTRET INC 32 THROWIF  // return if recv_internal, fail unless recv_external
    512 INT LDSLICEX DUP 32 PLDU   // sign cs cnt
    c4 PUSHCTR CTOS 32 LDU 256 LDU ENDS  // sign cs cnt cnt' pubk
    s1 s2 XCPU            // sign cs cnt pubk cnt' cnt
    EQUAL 33 THROWIFNOT   // ( seqno mismatch? )
    s2 PUSH HASHSU        // sign cs cnt pubk hash
    s0 s4 s4 XC2PU        // pubk cs cnt hash sign pubk
    CHKSIGNU              // pubk cs cnt ?
    34 THROWIFNOT         // signature mismatch
    ACCEPT
    SWAP 32 LDU NIP 8 LDU LDREF ENDS      // pubk cnt mode msg
    SWAP SENDRAWMSG       // pubk cnt ; ( message sent )
    INC NEWC 32 STU 256 STU ENDC c4 POPCTR
}>c
// code
<b 0 32 u, 
   newkeypair swap "wallet1" +suffix +".pk" B>file
   B, 
b> // data
Libs{
  x{ABACABADABACABA} s>c public_lib
  x{1234} x{5678} |_ s>c private_lib
}Libs  // libraries
GR$1.7 // balance
0 // split_depth
0 // ticktock
2 // mode: create
register_smc
dup make_special dup constant smc1_addr  
dup 256 u>B "wallet1" +suffix +".addr" B>file
."wallet address = " x. cr

// SmartContract #2 (Simple money giver for test network)
<{ SETCP0 DUP IFNOTRET INC 32 THROWIF  // return if recv_internal, fail unless recv_external
   32 LDU SWAP                         // cs cnt
   c4 PUSHCTR CTOS 32 LDU ENDS         // cs cnt cnt'
   TUCK EQUAL 33 THROWIFNOT            // ( seqno mismatch? )
   ACCEPT                              // cs cnt'
   SWAP 8 LDU LDREF ENDS               // cnt'' mode msg
   GR$20 INT 3 INT RAWRESERVE          // reserve all but 20 Grams from the balance
   SWAP SENDRAWMSG
   INC NEWC 32 STU ENDC c4 POPCTR      // store cnt''
}>c
// code
<b 0 32 u, b> // data
empty_cell    // libraries
GR$1000000    // initial balance (1m test Grams)
0 0 2 register_smc
dup make_special dup constant smc2_addr
dup 256 u>B "testgiver" +suffix +".addr" B>file
."free test gram giver address = " x. cr

// SmartContract #3

PROGRAM{
  NEWPROC store_grams
  recv_internal x{} PROC
  run_ticktock PROC:<{
    c4 PUSHCTR CTOS 32 LDU 256 LDU ENDS
    NEWC ROT INC 32 STUR OVER 256 STUR ENDC
    c4 POPCTR
    // first 32 bits of persistent data have been increased
    // remaining 256 bits with an address have been fetched
    // create new empty message with 0.1 Grams to that address
    NEWC b{00100010011111111} STSLICECONST TUCK 256 STU
    100000000 INT store_grams CALL  // store 0.1 Grams
    1 4 + 4 + 64 + 32 + 1+ 1+ INT STZEROES ENDC
    // send raw message from Cell
    ZERO SENDRAWMSG
    -17 INT 256 STIR 130000000 INT store_grams CALL
    107 INT STZEROES ENDC 
    ZERO // another message with 0.13 Grams to account -17
    NEWC b{11000100100000} "test" $>s |+ STSLICECONST
    123456789 INT store_grams CALL
    107 INT STZEROES "Hello, world!" $>s STSLICECONST ENDC
    ZERO SENDRAWMSG SENDRAWMSG // external message to address "test"
  }>
  store_grams PROC:<{
    -1 INT UNTIL:<{ INC 2DUP 3 LSHIFT# POW2 LESS }>
    ROT OVER 4 STUR SWAP 3 LSHIFT# STUX 
  }>
}END>c
// code
<b x{11EF55AA} s, smc1_addr 256 u, b> // data
// empty_cell // libraries
Libs{
  x{ABACABADABACABA} s>c public_lib
  x{1234} x{5678} |_ s>c public_lib
}Libs  // libraries
0x333333333 // balance
0 // split_depth
3 // ticktock: tick
2 // mode: create
register_smc
dup make_special dup constant smc2_addr
."address = " x. cr

// Configuration Parameters

// max-validators max-main-validators min-validators
// 9 3 2 config.validator_num!
1000 100 3 config.validator_num!
1 sg* 100 sg* 1000 sg* 1000000 sg* config.storage_prices!
config.special!

// gas_price gas_limit gas_credit block_gas_limit freeze_due_limit delete_due_limit -- 
1000 sg* 1000000 10000 10000000 GR$0.1 GR$1.0 config.gas_prices!
10000 sg* 1000000 10000 10000000 GR$0.1 GR$1.0 config.mc_gas_prices!
// lump_price bit_price cell_price ihr_factor first_frac next_frac
1000000 1000 sg* 100000 sg* 3/2 sg*/ 1/3 sg*/ 1/3 sg*/ config.fwd_prices!
10000000 10000 sg* 1000000 sg* 3/2 sg*/ 1/3 sg*/ 1/3 sg*/ config.mc_fwd_prices!
// mc-cc-lifetime sh-cc-lifetime sh-val-lifetime sh-val-num
250 250 1000 7 config.catchain_params!

"validator-keys" +suffix +".pub" file>B
{ dup Blen } { 32 B| swap dup ."Validator public key = " Bx. cr
  17 add-validator } while drop
// newkeypair nip dup ."Validator #1 public key = " Bx. cr 
// 17 add-validator
// newkeypair nip dup ."Validator #2 public key = " Bx. cr
// 239 add-validator
now dup 1000000 + 0 config.validators!

// SmartContract #3 (Config)

x{FF00} s>c // code
<b x{11EF} s, configdict ref, b> // data
empty_cell  // libraries
1  // balance
0 0 2 register_smc
dup set_config_smc
."address = " x. cr

// Other data

create_state
cr cr ."new state is:" cr dup <s csr. cr
dup 31 boc+>B dup Bx. cr
dup "zerostate" +suffix +".boc" B>file
Bhash ."file hash=" dup x. cr 256 u>B "zerostate" +suffix +".fhash" B>file
hash ."root hash=" dup x. cr 256 u>B "zerostate" +suffix +".rhash" B>file
